#!/usr/bin/gnuplot

reset

# Function with variation also in green
# randcolor(min,max,r) = (( ceil(((r-max)/(min-max))*255)) * 256 * 256 ) + (( ceil(((r-max)/(min-max))*255)) * 256 ) + ( floor(((r-min)/(max-min))* 255) ) 

# Function with no variation in green (Blue->Black, Black->Red)
randcolor(min,max,r) = (( ceil(((r-max)/(min-max))*255)) ) + ( floor(((r-min)/(max-min))* 255)*256*256) 

set  lmargin at screen 0.10
set  rmargin at screen 0.92
set  bmargin at screen 0.25
set  tmargin at screen 0.85
set  datafile separator ","
set  terminal png truecolor font 'Verdana,12' size 1160,800
set  title   "Precio medio por tienda"
set  ylabel  "Precio medio (euros)"
set  xlabel  "Tienda"
set  grid
set  boxwidth 0.80 relative
set  style fill transparent solid 1 noborder
set  xtic rotate by -45 scale 0 font ",12"

# First plot, just check output max, min values
set  output "/dev/null"
plot "./stats_meanprice.txt" using  ($0):2:($0):xticlabels(1) w boxes lc rgb variable notitle
max=GPVAL_DATA_Y_MAX
min=GPVAL_DATA_Y_MIN

# Final plot, just check output max, min values
set output "stats_meanprice_web.png"
plot './stats_meanprice.txt' using ($0):2:(randcolor(min,max,$2)):xticlabels(1) w boxes lc rgb variable notitle
